dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.5)

dnl Some handy macros
define([THE_PACKAGE_NAME],    [moot])
define([THE_PACKAGE_VERSION], [2.0.4-pre1])
define([THE_PACKAGE_MAINTAINER],  [moocow@ling.uni-potsdam.de])

AC_INIT(THE_PACKAGE_NAME, THE_PACKAGE_VERSION, THE_PACKAGE_MAINTAINER)
dnl AC_INIT(moot, 2.0.0, moocow@ling.uni-potsdam.de)

dnl source && aux dir
AC_CONFIG_AUX_DIR(config)

dnl canonical target (sets $target, $target_(cpu|vendor|os) : used for bindist)
AC_CANONICAL_TARGET

dnl use automake
AM_INIT_AUTOMAKE(THE_PACKAGE_NAME, THE_PACKAGE_VERSION)

dnl use autoheader
AM_CONFIG_HEADER([src/libmoot/mootConfig.h])

dnl default prefix
AC_PREFIX_DEFAULT(/usr/local)

#-------------------------------------------------------------
# save user's *FLAGS
#USER_LIBS="$LIBS"
#USER_LDFLAGS="$LDFLAGS"
#USER_CPPFLAGS="$CPPFLAGS"
USER_CXXFLAGS="$CXXFLAGS"

dnl
dnl check for programs
dnl
AC_PROG_CC
AC_PROG_CXX
AC_LANG(C++)


##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## debug ?
##
CONFIG_OPTIONS=""
AC_MSG_CHECKING([whether to build debug version])
AC_ARG_ENABLE(debug,
	AC_HELP_STRING([--enable-debug], [build debug version (default=no)]))

if test "$enable_debug" == "yes" ; then
   AC_MSG_RESULT(yes)
   AC_DISABLE_SHARED
   moot_OFLAGS="-g"
   #CONFIG_OPTIONS="$CONFIG_OPTIONS DEBUG=1"
   CONFIG_OPTIONS="DEBUG=1"
else
  AC_MSG_RESULT(no)
  #moot_OFLAGS="-O2"
  #moot_OFLAGS="-O3 -fomit-frame-pointer -funroll-loops -finline-limit-100000"
  moot_OFLAGS="-O3 -fomit-frame-pointer -funroll-loops"
  #CONFIG_OPTIONS="$CONFIG_OPTIONS DEBUG=0"
  CONFIG_OPTIONS="DEBUG=0"
fi

AC_SUBST(moot_OFLAGS)
##
## /debug ?
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


dnl v--- needed if Makefile.am uses _LTLIBRARIES targets
AC_PROG_LIBTOOL

dnl v--- for static-only libraries (non-libtool)
dnl AC_PROG_RANLIB

dnl v--- needed if Makefile.am uses lex sources
dnl AM_PROG_LEX


### --- unmangle user's CXXFLAGS
if test "$CXXFLAGS" != "$USER_CXXFLAGS" ; then
  # autoconf likes to set this (to '-g -O2')... who knows why...
  AC_MSG_NOTICE([Restoring user's original CXXFLAGS value])
  CXXFLAGS="$USER_CXXFLAGS"
fi

#---------------------------------------------------------------
# short package includes
spkgincludedir="\${includedir}/AC_PACKAGE_NAME"
AC_SUBST(spkgincludedir)
# short package includes
#---------------------------------------------------------------

#---------------------------------------------------------------
# get real prefix
AC_CACHE_CHECK([for installation prefix], [ac_cv_install_prefix],
	[if test "$prefix" = "NONE" ; then
	   ac_cv_install_prefix="/usr/local"
	 else
           ac_cv_install_prefix="$prefix"
	 fi
	])
dnl #  ... and add it to our flags
dnl CPPFLAGS="$CPPFLAGS -I${ac_cv_install_prefix}/include"
dnl LDFLAGS="$LDFLAGS -L${ac_cv_install_prefix}/lib"


dnl check for strdup
AC_CHECK_FUNC(strdup,[AC_DEFINE(HAVE_STRDUP,1,[Define this if you have the strdup() function])])


dnl ---------------------------------------------------------------
dnl pkg-config : program
dnl
AC_ARG_VAR(PKG_CONFIG, [How to run the pkg-config program])
AC_ARG_VAR(PKG_CONFIG_PATH, [Directories to search for pkg-config])
if test -z "$PKG_CONFIG" ; then
  AC_PATH_PROG(PKG_CONFIG,pkg-config,[])
fi
dnl pkg-config: destination directory
AC_ARG_WITH(pkgconfig-dir,
	AC_HELP_STRING([--with-pkgconfig-dir=DIR],
		[install pkg-config metafile(s) in DIR (default=LIBDIR/pkgconfig)]),
	[ac_cv_pkgconfigdir="$withval"])
if test -z "$ac_cv_pkgconfigdir" ; then
  ac_cv_pkgconfigdir="\$(libdir)/pkgconfig"
fi
pkgconfigdir="$ac_cv_pkgconfigdir"
AC_SUBST(pkgconfigdir)
dnl
dnl pkg-config
dnl ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^



dnl vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
dnl check for time headers (argh)
AC_CHECK_HEADERS([time.h sys/time.h],
	[],
	[AC_MSG_WARN([Required header file not found!])
    	 AC_MSG_WARN([Are the 'struct timeval' and 'time()' headers in your 'CPPFLAGS' environment variable?])
    	 AC_MSG_ERROR([Required headers not found -- aborting.])
	])
dnl time headers
dnl ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

dnl vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
dnl check for zlib
dnl
dnl zlib: user-request
AC_ARG_ENABLE(zlib,
	AC_HELP_STRING([--disable-zlib],
	               [Disable zlib compression support]),
	[ac_cv_enable_zlib="$enableval"],[ac_cv_enable_zlib="yes"])

if test "$ac_cv_enable_zlib" != "no" ; then
 dnl zlib: headers
 for h in zlib.h ; do
  AC_CHECK_HEADER($h,
	[moot_have_header="yes"], [moot_have_header="no"], [ ])
  if test "$moot_have_header" = "no" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([                 zlib header '$h' not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([    Is the directory containing the zlib headers in your])
    AC_MSG_WARN([             'CPPFLAGS' environment variable?])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
    ac_cv_enable_zlib="no"
  fi
 done
fi

if test "$ac_cv_enable_zlib" != "no" ; then
 dnl zlib: library: compile
 AC_CHECK_LIB(z,deflate,[ac_cv_have_libz="yes"])
 if test "$ac_cv_have_libz" != "yes" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([                   zlib library not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([         Is the directory containing libz.a in your])
    AC_MSG_WARN([                'LDFLAGS' environment variable?])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
    ac_cv_enable_zlib="no"
 else
   moot_LIBS="$moot_LIBS -lz"
 fi
fi

##-- zlib: config.h flag
if test "$ac_cv_enable_zlib" = "no" ; then
  AC_MSG_NOTICE([zlib compression support disabled.])
  CONFIG_OPTIONS="$CONFIG_OPTIONS ZLIB=0"
else
 ##-- ac_cv_enable_zlib != no
 AC_DEFINE(MOOT_ZLIB_ENABLED,1,
	   [Define this to enable zlib compression support])
 DOXY_DEFINES="$DOXY_DEFINES MOOT_ZLIB_ENABLED=1"
 CONFIG_OPTIONS="$CONFIG_OPTIONS ZLIB=1"
fi
dnl
dnl zlib
dnl ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


dnl ------------------------------------------------------------
dnl check for flex++bison++
dnl
AC_ARG_VAR(FLEXXX, [Path to Alain Coetmeur's flex++ program.])
AC_ARG_VAR(BISONXX, [Path to Alain Coetmeur's bison++ program.])

PKG_CHECK_MODULES(FBXX, flex++bison++ >= 0.0.5,
	[ac_cv_have_pkg_fbxx="yes"],
	[ac_cv_have_pkg_fbxx="no"])

if test "$ac_cv_have_pkg_fbxx" = "yes"; then
  PATH="$PATH:`$PKG_CONFIG --variable=prefix flex++bison++`/bin"
  export PATH
  ###--- libFSMext would already get us this, but it's being removed ---
  CPPFLAGS="$CPPFLAGS `$PKG_CONFIG --cflags-only-I flex++bison++`"
fi


dnl ------------------------------------------------------------
dnl check for flex++
dnl
if test -z "$FLEXXX" ; then
  if test "$ac_cv_have_pkg_fbxx" = "yes"; then
    FLEXXX="`$PKG_CONFIG --variable=prefix flex++bison++`/bin/flex++"
    test -x "$FLEXXX" || FLEXXX=""
  fi
  if test -z "$FLEXXX" ; then
    AC_PATH_PROG(FLEXXX,[flex++],[])
  fi
  if test -z "$FLEXXX" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([                Coetmeur's flex++ not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([         Does your FLEXXX environment variable contain])
    AC_MSG_WARN([              the full path to the flex++ program?])
    AC_MSG_WARN([])
    AC_MSG_WARN([                  Your build might fail!])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
  fi
fi


dnl ------------------------------------------------------------
dnl check for bison++
dnl
if test -z "$BISONXX" ; then
  if test "$ac_cv_have_pkg_fbxx" = "yes" ; then
    BISONXX="`$PKG_CONFIG --variable=prefix flex++bison++`/bin/bison++"
    test -x "$BISONXX" || BISONXX=""
  fi
  if test -z "$BISONXX" ; then
    AC_PATH_PROG(BISONXX,[bison++],[])
  fi
  if test -z "$BISONXX" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([                Coetmeur's bison++ not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([         Does your BISONXX environment variable contain])
    AC_MSG_WARN([              the full path to the bison++ program?])
    AC_MSG_WARN([])
    AC_MSG_WARN([                  Your build might fail!])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
  fi
fi
YACC="$BISONXX"
AC_SUBST(YACC)
AC_SUBST(BISONXX)

dnl vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
dnl check for recode
dnl
dnl recode: user-request
AC_ARG_ENABLE(recode,
	AC_HELP_STRING([--disable-recode],
	               [Disable librecode support]),
	[ac_cv_enable_recode="$enableval"],[ac_cv_enable_recode="yes"])

dnl recode: headers
if test "$ac_cv_enable_recode" != "no" ; then
 for h in recodext.h ; do
  AC_CHECK_HEADER($h,
	[moot_have_header="yes"], [moot_have_header="no"], [])
  if test "$moot_have_header" = "no" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([             librecode header '$h' not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([    Is the directory containing the librecode headers in your])
    AC_MSG_WARN([             'CPPFLAGS' environment variable?])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
    ac_cv_enable_recode="no"
  fi
 done
fi ;##-- /ac_cv_enable_recode != no

dnl recode: library: compile
if test "$ac_cv_enable_recode" != "no" ; then

 AC_CHECK_LIB(recode,recode_new_outer,[ac_cv_have_librecode="yes"])

 if test "$ac_cv_have_librecode" != "yes" ; then
   AC_MSG_WARN([])
   AC_MSG_WARN([----------------------------------------------------------------])
   AC_MSG_WARN([             librecode library not found!])
   AC_MSG_WARN([])
   AC_MSG_WARN([    Is the directory containing the librecode.a in your])
   AC_MSG_WARN([             'LDFLAGS' environment variable?])
   AC_MSG_WARN([----------------------------------------------------------------])
   AC_MSG_WARN([])
   ac_cv_enable_recode="no"
 else
  moot_LIBS="$moot_LIBS -lrecode"
 fi
fi ;##-- /ac_cv_enable_recode != no

dnl recode: config.h flag
if test "$ac_cv_enable_recode" = "no" ; then
  AC_MSG_NOTICE([librecode support disabled.])
  CONFIG_OPTIONS="$CONFIG_OPTIONS RECODE=0"
else
 ##-- ac_cv_enable_recode != no
 AC_DEFINE(MOOT_RECODE_ENABLED,1,
	   [Define this to enable librecode support (useful for XML)])
 DOXY_DEFINES="$DOXY_DEFINES MOOT_RECODE_ENABLED=1"
 CONFIG_OPTIONS="$CONFIG_OPTIONS RECODE=1"
fi
dnl recode
dnl ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


dnl vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
dnl check for expat
dnl
dnl expat: user-request
AC_ARG_ENABLE(expat,
	AC_HELP_STRING([--disable-expat],
	               [Disable experimental expat XML support]),
	[ac_cv_enable_expat="$enableval"],[ac_cv_enable_expat="yes"])

dnl expat: headers
if test "$ac_cv_enable_expat" != "no" ; then
 for h in expat.h ; do
  AC_CHECK_HEADER($h,
	[moot_have_header="yes"], [moot_have_header="no"], [ ])
  if test "$moot_have_header" = "no" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([             expat header '$h' not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([    Is the directory containing the expat headers in your])
    AC_MSG_WARN([             'CPPFLAGS' environment variable?])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
    ac_cv_enable_expat="no"
  fi
 done
fi ;##-- /ac_cv_enable_expat != no

dnl expat: library: compile
if test "$ac_cv_enable_expat" != "no" ; then

 AC_CHECK_LIB(expat,XML_DefaultCurrent,[ac_cv_have_libexpat="yes"])

 if test "$ac_cv_have_libexpat" != "yes" ; then
    AC_MSG_WARN([])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([                expat library not found!])
    AC_MSG_WARN([])
    AC_MSG_WARN([      Is the directory containing libexpat.a in your])
    AC_MSG_WARN([             'LDPFLAGS' environment variable?])
    AC_MSG_WARN([----------------------------------------------------------------])
    AC_MSG_WARN([])
    ac_cv_enable_expat="no"
 else
    moot_LIBS="$moot_LIBS -lexpat"
 fi
fi ;##-- /ac_cv_enable_xml != no

dnl expat: config.h flag
if test "$ac_cv_enable_expat" = "no" ; then
  AC_MSG_NOTICE([Expat XML support disabled.])
  CONFIG_OPTIONS="$CONFIG_OPTIONS EXPAT=0"
else
 ##-- ac_cv_enable_xml != no
 AC_DEFINE(MOOT_EXPAT_ENABLED,1,
	   [Define this to enable experimental expat XML support])
 DOXY_DEFINES="$DOXY_DEFINES MOOT_EXPAT_ENABLED=1"
 CONFIG_OPTIONS="$CONFIG_OPTIONS EXPAT=1"
fi
dnl expat
dnl ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


dnl vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
dnl check for libxml2
dnl
dnl libxml2: user-request
AC_ARG_ENABLE(libxml,
	AC_HELP_STRING([--enable-libxml],
	               [Enable experimental libxml2 XML support (default=no)]),
	[ac_cv_enable_libxml="$enableval"],[ac_cv_enable_libxml="no"])

dnl libxml2: pkg-config
if test "$ac_cv_enable_libxml" != "no" ; then
  PKG_CHECK_MODULES(LIBXML2, libxml-2.0 >= 2.6.7,
	[ac_cv_enable_libxml="yes"],
	[ac_cv_enable_libxml="no"])
fi

dnl libxml: config.h flag
if test "$ac_cv_enable_libxml" = "no" ; then
  AC_MSG_NOTICE([libxml2 XML support disabled.])
 CONFIG_OPTIONS="$CONFIG_OPTIONS LIBXML2=0"
else
 ##-- ac_cv_enable_xml != no
 AC_DEFINE(MOOT_LIBXML_ENABLED,1,
	   [Define this to enable experimental libxml2 XML support])
 CXXFLAGS="$CXXFLAGS $LIBXML2_CFLAGS"
 LDFLAGS="$LDFLAGS `$PKG_CONFIG --libs-only-L libxml-2.0`"
 moot_LIBS="$moot_LIBS `$PKG_CONFIG --libs-only-l libxml-2.0`"

 DOXY_DEFINES="$DOXY_DEFINES MOOT_LIBXML_ENABLED=1"
 CONFIG_OPTIONS="$CONFIG_OPTIONS LIBXML2=1"
fi
## /libxml2
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^



##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## check for optgen.perl
##
AC_ARG_VAR(OPTGEN_PERL, [Path to the 'optgen.perl' script.])

if test -z "$OPTGEN_PERL" ; then
  AC_PATH_PROG(OPTGEN_PERL,[optgen.perl],[])
  if test -z "$OPTGEN_PERL" ; then
    AC_MSG_RESULT([not found])
    AC_MSG_WARN([])
    AC_MSG_WARN([cannot find optgen.perl program -- CVS builds will fail!])
    AC_MSG_WARN([])
  fi
fi
AC_SUBST(OPTGEN_PERL)
##
## /check for optgen.perl
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^



##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## documentation
##
AC_ARG_WITH(docdir,
	AC_HELP_STRING([--with-docdir=DIR],
		[install documentation in DIR/AC_PACKAGE_NAME (default=DATADIR/doc)]),
	[docdir="$withval"],
	[docdir="\${datadir}/doc"])
pkgdocdir="\${docdir}/\${PACKAGE}"
pkgdocprogdir="\${docdir}/\${PACKAGE}/programs"

AC_SUBST(docdir)
AC_SUBST(pkgdocdir)
AC_SUBST(pkgdocprogdir)

AC_ARG_WITH(doc-formats,
	AC_HELP_STRING([--with-doc-formats=LIST],
		       [Build documentation formats in LIST. \
                        Available formats: txt, man, html, dvi, ps, pdf, none.
	                Default='man html']),
	[ac_cv_doc_formats="$withval"])

## -- set default doc formats if unspecified
if test -z "$ac_cv_doc_formats" ; then
  ac_cv_doc_formats="man html"
fi

## -- un-comma-tize the doc-formats
moot_doc_formats=`echo "$ac_cv_doc_formats" | sed 's/\,/ /g'`


AC_ARG_ENABLE(doc,
	AC_HELP_STRING([--disable-doc],[Synonym for --with-doc-formats="none"]),
	[enable_doc="$enableval"],[enable_doc="yes"])


AC_MSG_CHECKING([which documentation formats to build])
if test "$enable_doc" != "yes" ; then
 ##-- disable all docs
 moot_doc_formats="none"
fi
AC_MSG_RESULT($moot_doc_formats)
#echo ">> post RESULT"


if test "$moot_doc_formats" != "none" ; then
  ### docs: prog doxygen
  AC_PATH_PROG(DOXYGEN,doxygen,[])
  AC_SUBST(DOXYGEN)
  AC_SUBST(DOXYGEN_SOURCES)

  ### -- report doxygen-check results
  if test -z "$DOXYGEN" ; then
    AC_MSG_WARN([doxygen not found: library documentation will not be built!])
  fi

  ### docs: pod converters
  AC_PATH_PROG(POD2TEXT,pod2text,[])
  AC_PATH_PROG(POD2MAN,pod2man,[])
  AC_PATH_PROG(POD2HTML,pod2html,[])
  AC_PATH_PROG(POD2LATEX,pod2latex,[])
  AC_SUBST(POD2TEXT)
  AC_SUBST(POD2MAN)
  AC_SUBST(POD2HTML)
  AC_SUBST(POD2LATEX)

  ### -- docs: .gog file linkup
  #moot_program_gogs="moot.gog mootpp.gog moot-fstgen.gog moot-pargen.gog"
  #moot_program_gogs="mootpp.gog mootrain.gog moothmm.gog mootm.gog moot.gog"
  #moot_program_gogs="mootpp.gog mootrain.gog moothmm.gog moot.gog"
  moot_program_gogs="\
	mootconfig.gog \
	mootpp.gog \
	mootrain.gog \
	mootcompile.gog \
	mootdump.gog \
	moot.gog \
	mooteval.gog \
	mootchurn.gog \
	"
  for g in $moot_program_gogs ; do
    AC_CONFIG_LINKS(doc/programs/${g}:src/programs/${g})  
  done
  AC_SUBST(moot_program_gogs)
  AC_CONFIG_LINKS(doc/programs/acknowledge.pod:src/programs/acknowledge.pod)

  ##-- index-skeleton
  DOC_PROG_INDEX_SKEL="mootutils.skel"
  DOC_PROG_INDEX_POD="mootutils.pod"
  DOC_PROG_INDEX_MAN="mootutils.1"
  AC_SUBST(DOC_PROG_INDEX_SKEL)

  ##-- hack, must add section-5 stuff below in 'mans' section too!
  moot_pods="mootfiles.pod mootutils.pod `echo \"$moot_program_gogs\" | sed 's/\.gog/\.pod/g'`"
  AC_SUBST(moot_pods)

  ### -- initialize defaults
  DOC_SUBDIRS=""
  DOC_LATEX_TARGETS=""
  DOXY_WANT_HTML="NO"
  DOXY_WANT_MAN="NO"
  DOXY_WANT_LATEX="NO"

  for fmt in $moot_doc_formats ; do
    case "$fmt" in

      txt)
	if test -n "$POD2TEXT" ; then
	  DOC_PROG_TXT_TARGETS=`echo "$moot_pods" | sed 's/\.pod/\.txt/g'`
	else
	  AC_MSG_WARN(pod2text not found -- program text docs will not be built!)
	fi
	;;

      man)
	if test -n "$DOXYGEN" ; then
	  DOXY_WANT_MAN="YES"
          DOC_MANDIRS="man/man3"
          DOC_MAN3_MANS="man/man3/*"
	fi
	if test -n "$POD2MAN" ; then
	  DOC_PROG_MAN1_TARGETS="$DOC_PROG_INDEX_MAN \
	                        `echo \"$moot_program_gogs\" \
	                         | sed 's/\.gog/\.1/g'`"
	  DOC_PROG_MAN5_TARGETS="mootfiles.5"
	else
	  AC_MSG_WARN(pod2man not found -- program manpages will not be built!)
	fi
	;;

      html)
	if test -n "$DOXYGEN" ; then
          DOXY_WANT_HTML="YES"
          DOC_SUBDIRS="$DOC_SUBDIRS html"
	fi
	if test -n "$POD2HTML" ; then
	  DOC_PROG_HTML_TARGETS=`echo "$moot_pods" | sed 's/\.pod/\.html/g'`
	else
	  AC_MSG_WARN(pod2html not found -- program html docs will not be built!)
	fi
	;;

      dvi)
	if test -n "$DOXYGEN" ; then
	  DOXY_WANT_LATEX="YES"
	  DOC_LATEX_TARGETS="${DOC_LATEX_TARGETS} refman.dvi"
	fi
	if test -n "$POD2LATEX" ; then
	  DOC_PROG_DVI_TARGETS=`echo "$moot_pods" | sed 's/\.pod/\.dvi/g'`
	else
	  AC_MSG_WARN(pod2latex not found -- program dvi docs will not be built!)
	fi
	;;

      ps)
	if test -n "$DOXYGEN" ; then
	  DOXY_WANT_LATEX="YES"
	  DOC_LATEX_TARGETS="${DOC_LATEX_TARGETS} refman.ps"
	fi
	if test -n "$POD2LATEX" ; then
	  DOC_PROG_PS_TARGETS=`echo "$moot_pods" | sed 's/\.pod/\.ps/g'`
	else
	  AC_MSG_WARN(pod2latex not found -- program postscript docs will not be built!)
	fi
	;;

      pdf)
	if test -n "$DOXYGEN" ; then
	  DOXY_WANT_LATEX="YES"
    	  DOC_LATEX_TARGETS="${DOC_LATEX_TARGETS} refman.pdf"
	fi
	if test -n "$POD2LATEX" ; then
	  DOC_PROG_PDF_TARGETS=`echo "$moot_pods" | sed 's/\.pod/\.pdf/g'`
	else
	  AC_MSG_WARN(pod2latex not found -- program pdf docs will not be built!)
	fi
	;;

      *)
	AC_MSG_WARN(ignoring unknown documentation format: $fmt)
	;;
    esac; # case "$fmt" in ...
  done; # for fmt in $moot_doc_formats ...

  AC_SUBST(DOXY_DEFINES)

  AC_SUBST(DOXY_WANT_HTML)
  AC_SUBST(DOXY_WANT_MAN)
  AC_SUBST(DOXY_WANT_LATEX)

  AC_SUBST(DOC_SUBDIRS)
  AC_SUBST(DOC_MANDIRS)
  AC_SUBST(DOC_MAN3_MANS)
  AC_SUBST(DOC_LATEX_TARGETS)
  AC_SUBST(POD_DOC_SUFFIXES)

  AC_SUBST(DOC_PROG_TXT_TARGETS)
  AC_SUBST(DOC_PROG_MAN1_TARGETS)
  AC_SUBST(DOC_PROG_MAN5_TARGETS)
  AC_SUBST(DOC_PROG_HTML_TARGETS)
  AC_SUBST(DOC_PROG_DVI_TARGETS)
  AC_SUBST(DOC_PROG_PS_TARGETS)
  AC_SUBST(DOC_PROG_PDF_TARGETS)


  ##-- check for tag-files (this needs an overhaul!)
  #for ext in mootm ; do
  #  extdocdir="`$PKG_CONFIG --variable=pkgdocdir ${ext}`"
  #  if test -n "$extdocdir" -a "$extdocdir" != "no" ; then
  #    exttagfiles="`find $extdocdir -name '*.tag'`"
  #    for exttag in $exttagfiles ; do
  #      exttagdir="`dirname $exttag`/html"
  #      if test -d "$exttagdir" ; then
  #        DOXY_TAGFILES="$DOXY_TAGFILES $exttag=$exttagdir"
  #      fi
  #    done
  #  fi
  #done 
  AC_SUBST(DOXY_TAGFILES)   
fi; # if "$moot_doc_formats" != "none" ...
##
## /Documentation
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^



#---------------------------------------------------------------
# Binary distribution
BINDIST_RELEASE=1

## -- hack: canonicalize package-name to lower-case (for debian)
BINDIST_PKGNAME=`echo "$PACKAGE" | tr '[[:upper:]]' '[[:lower:]]'`

## -- hack: downgrade ix86 -> i386
case "$target_cpu" in
  i[[3-9]]86)
	BINDIST_CPU=i386
	;;
  *)
	BINDIST_CPU="$target_cpu"
	;;
esac
BINDIST_OS="$target_os"

AC_SUBST(BINDIST_PKGNAME)
AC_SUBST(BINDIST_RELEASE)
AC_SUBST(BINDIST_CPU)
AC_SUBST(BINDIST_OS)
# Binary distribution
#---------------------------------------------------------------


dnl ---------------------------------------------------------------
dnl cxxflags: template depth
dnl
CXXFLAGS="$CXXFLAGS -ftemplate-depth-24"
dnl
dnl cxxflags: template depth
dnl ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## trigrams ?
##
AC_MSG_CHECKING([whether to use trigrams])
AC_ARG_ENABLE(trigrams,
	AC_HELP_STRING([--enable-trigrams], [Use trigrams (default=no)]),
	[ac_cv_enable_trigrams="$enableval"],[ac_cv_enable_trigrams="no"])

if test "$ac_cv_enable_trigrams" != "no" ; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(MOOT_USE_TRIGRAMS,1,
	    [Define this to enable use of trigrams])
  CONFIG_OPTIONS="$CONFIG_OPTIONS USE_TRIGRAMS=1"
else
  AC_MSG_RESULT(no)
  CONFIG_OPTIONS="$CONFIG_OPTIONS USE_TRIGRAMS=0"
fi
##
## /trigrams ?
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## trigram-hash ?
##
AC_MSG_CHECKING([whether to hash trigrams])
AC_ARG_ENABLE(trigram-hash,
	AC_HELP_STRING([--enable-trigram-hash],
	               [Save space but lose speed by hashing trigram (default=no)]),
	[ac_cv_enable_trigram_hash="$enableval"],[ac_cv_enable_trigram_hash="no"])

if test "$ac_cv_enable_trigrams" != "no" -a "$ac_cv_enable_trigram_hash" != "no" ; then
   AC_MSG_RESULT(yes)
   AC_DEFINE(MOOT_HASH_TRIGRAMS,1,
 	    [Define this to enable slow but memory-friendly trigram hash])
   CONFIG_OPTIONS="$CONFIG_OPTIONS HASH_TRIGRAMS=1"
else
   AC_MSG_RESULT(no)
   CONFIG_OPTIONS="$CONFIG_OPTIONS HASH_TRIGRAMS=0"
fi
##
## /trigram-hash ?
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## relax ?
##
AC_MSG_CHECKING([whether to relax])
AC_ARG_ENABLE(relax,
	AC_HELP_STRING([--enable-relax], [Enable relaxed interpretation of pre-analyses (default=no)]),
	[ac_cv_enable_relax="$enableval"],[ac_cv_enable_relax="no"])

if test "$ac_cv_enable_relax" != "no" ; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(MOOT_RELAX,1, [Define this to enable relaxed interpretation of pre-analyses])
  CONFIG_OPTIONS="$CONFIG_OPTIONS RELAX=1"
else
  AC_MSG_RESULT(no)
  CONFIG_OPTIONS="$CONFIG_OPTIONS RELAX=0"
fi
##
## /relax ?
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## warnings ?
##
AC_MSG_CHECKING([whether to display compiler warnings])
AC_ARG_ENABLE(warnings,
	AC_HELP_STRING([--disable-warnings],[disable compiler warnings (default=no)]))

if test "$enable_warnings" != "no" ; then
   AC_MSG_RESULT(yes)
   moot_WFLAGS="-Wall"
else
  AC_MSG_RESULT(no)
  moot_WFLAGS=""
fi
AC_SUBST(moot_WFLAGS)
##
## /warnings
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


##vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
## configuration options
##
AC_SUBST(CONFIG_OPTIONS)
AC_SUBST(moot_LIBS)
##
## /config options
##^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^



dnl
dnl output
dnl
AC_CONFIG_FILES(src/libmoot/Makefile)
AC_CONFIG_FILES(src/programs/Makefile)
AC_CONFIG_FILES(src/programs/mootconfig, [chmod 0755 src/programs/mootconfig])
AC_CONFIG_FILES(src/Makefile)
dnl AC_CONFIG_FILES(doc/libmoot/mainpage.dox)
dnl AC_CONFIG_FILES(doc/libmoot/doxy-filter.cfg)
AC_CONFIG_FILES(doc/libmoot/libmoot.doxy doc/libmoot/Makefile)
AC_CONFIG_FILES(doc/programs/Makefile)
AC_CONFIG_FILES(doc/Makefile)
AC_CONFIG_FILES(config/Makefile)
AC_CONFIG_FILES(Makefile)
AC_CONFIG_FILES(moot.pc)
AC_CONFIG_FILES(moot.spec)
AC_OUTPUT
